/**
 * To download necessary library from HuaWei server.
 * Including mindspore-lite .so file, minddata-lite .so file and model file.
 * The libraries can be downloaded manually.
 */
def targetModelFile = "src/main/assets/segment_model.ms"
def modelDownloadUrl = "https://download.mindspore.cn/model_zoo/official/lite/mobile_segment_lite/segment_model.ms"

task downloadModelFile(type: DownloadUrlTask) {
    doFirst {
        println "Downloading ${modelDownloadUrl}"
    }
    sourceUrl = "${modelDownloadUrl}"
    target = file("${targetModelFile}")
}

if (file("src/main/assets/segment_model.ms").exists()) {
    downloadModelFile.enabled = false
}

preBuild.dependsOn downloadModelFile

class DownloadUrlTask extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}
